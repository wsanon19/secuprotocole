(* Description of the protocol:

			1. A → S : ⟨A, {B}ˢₖ₍ₛₐ₎⟩

      2. S → B : ⟨S, {⟨A, Kₛ⟩}ˢₖ₍ₚₛ₎⟩

			3. S → A : ⟨S, {⟨B, Kₛ⟩}ˢₖ₍ₐₛ₎⟩

			4. A → B : ⟨A, {K}ˢₖₛ, {Nₐ}ᵃₚₖ₍ₚ₎⟩

			5. B → A : ⟨B, {⟨Nₐ, Nₚ⟩}ˢₖ⟩

			6. A → B : ⟨A, {Nₚ}ˢₖ⟩

*)

(************************* DEFINITIONS *********************************)

(* A unique piece of data identifying who you really are. *)
type identity.
fun rawidentity(identity) : bitstring [data].

(* The types of key pairs used for asymmetric encryption. *)
type pubkey.
type seckey.
(* Functions used to access public and private keys, which are unforgeable. *)
fun pub(identity) : pubkey [private].
fun sec(identity) : seckey [private].
(* For the sake of type conversion, so that we can identify a public key as an arbitrary sequence of bits. *)
fun rawpubkey(pubkey) : bitstring [data].
(* Encryption and decryption using public and private keys. *)
fun aenc(bitstring, pubkey) : bitstring.
fun adec(bitstring, seckey) : bitstring
  reduc forall m : bitstring, i : identity; adec(aenc(m, pub(i)), sec(i)) = m.

(* The type of key used for symmetric encryption. *)
type symkey.
fun rawsymkey(symkey) : bitstring [data].
(* Encryption and decryption using symmetric keys. *)
fun senc(bitstring, symkey) : bitstring.
fun sdec(bitstring, symkey) : bitstring 
  reduc forall m : bitstring, k : symkey; sdec(senc(m, k), k) = m.

(* Hypothesis: assume that any honest peer knows about a private symmetric key to talk to the server. *)
fun sskey(identity, identity) : symkey [private].

(* This function represents an arbitrary hash function, to be specified somewhere during implementation.
   It is left abstract here because we don't really care, although we assume that it is perfect,
   meaning that h(A) = h(B) iff A = B. *)
fun h(bitstring) : bitstring.

(* The abstract type of nonces, otherwise known as random bitstrings. *)
type nonce.

(************************** EVENTS *****************************)

(* Triggered when a protocol run started (after a very simple handshake). *)
event alice_start(identity, identity).
(* Triggered when Alice ended a protocol run. *)
event alice_end(identity, identity, symkey).
(* Triggered when Bob ended a protocol run. *)
event bob_end(identity, identity, symkey).
(* Triggered when Alice sent a key onto the network. *)
event alice_sent(identity, identity, symkey).
(* Triggered when Bob received a key he can decrypt from the network. *)
event bob_received(identity, identity, symkey).
(* Triggered when the server relays the public key of Bob to Alice. *)
event server_sent_pubkey(identity, pubkey).
(* Triggered when Alice receives the public key from the server. *)
event alice_received_pubkey(pubkey).
(* Triggered when Bob sent his public key to the server. *)
event bob_sent_pubkey(pubkey).

event holds_secret(identity, identity, symkey).

event server_sent_key(identity, symkey).

event alice_received_key(identity, symkey).

event bob_received_key(identity, symkey).

(************************** COMMON STUFF *****************************)

(* The network onto which Alice and Bob want to talk secretly. *)
channel network.

(*************************** PEERS ********************************)

let ALICE(a : identity, b : identity, s : identity) =
  event alice_start(a, b);
  let K_AS = sskey(a, s) in 

  out(network, (a, senc(rawidentity(b), K_AS)));

  in(network, (=s, b_ks_senc : bitstring));
  let (=b, K_S : symkey) = sdec(b_ks_senc, K_AS) in
  event alice_received_key(a, K_S);
  
  new K : symkey;
  event holds_secret(a, b, K);
  new N_A : bitstring;
  event alice_sent(a, b, K);
  out(network, (a, senc(rawsymkey(K), K_S), aenc(N_A, pub(b))));

  in(network, (=b, na_nb_senc : bitstring));
  let (=N_A, N_B : bitstring) = sdec(na_nb_senc, K) in 
  out(network, (a, senc(N_B, K)));

  event alice_end(a, b, K);
  0.

let BOB(b : identity, s : identity) =
  let K_BS = sskey(b, s) in

  in(network, (=s, a_ks_senc : bitstring));
  let (a : identity, K_S : symkey) = sdec(a_ks_senc, K_BS) in 
  event bob_received_key(b, K_S);

  in(network, (=a, k_senc : bitstring, na_aenc : bitstring));
  let rawsymkey(K) = sdec(k_senc, K_S) in
  event bob_received(a, b, K);
  let N_A : bitstring = adec(na_aenc, sec(b)) in 
  new N_B : bitstring;
  out(network, (b, senc((N_A, N_B), K)));

  in(network, (=a, nb_senc : bitstring));
  let (=N_B) = sdec(nb_senc, K) in

  event bob_end(a, b, K);
  0.

let SERVER(a : identity, b : identity, s : identity) =
  let K_AS = sskey(a, s) in 
  let K_BS = sskey(b, s) in

  in(network, (=a, b_senc : bitstring));
  let rawidentity(=b) = sdec(b_senc, K_AS) in

  new K_S : symkey;
  event server_sent_key(b, K_S);
  out(network, (s, senc((a, K_S), K_BS)));
  event server_sent_key(a, K_S);
  out(network, (s, senc((b, K_S), K_AS)));

  0.
